load("../units.zen", "PowerRange", "Voltage", "VoltageRange", "CurrentRange")
load("../interfaces.zen", "Power", "Ground")
load("../utils.zen", "format_value")

# Package      JEDEC         LxWxH
# SMF          DO-219AB      3.0×1.8×1.0
# SMA          DO-214AC      4.6×2.6×2.1
# SMB          DO-214AA      5.3×3.7×2.5
# SMC          DO-214AB      7.8×6.8×2.5
Package = enum("DO-219AB", "DO-214AC", "DO-214AA", "DO-214AB")

USB_5V_STANDOFF_VOLTAGE = VoltageRange("5 to 6V")
USB_5V_CLAMPING_VOLTAGE = VoltageRange("9 to 12V")
USB_5V_POWER_PP = PowerRange("800 to 1200W")

package = config("package", Package, default=Package("DO-219AB"))
# 8/20 μs waveform acc. IEC 61000-4-5
reverse_standoff_voltage = config("reverse_standoff_voltage", VoltageRange, default=USB_5V_STANDOFF_VOLTAGE)
reverse_clamping_voltage = config("reverse_clamping_voltage", VoltageRange, default=USB_5V_CLAMPING_VOLTAGE)
peak_pulse_power = config("peak_pulse_power", PowerRange, default=USB_5V_POWER_PP)

mpn = config("mpn", str, optional=True)
manufacturer = config("manufacturer", str, optional=True)

A = io("A", Ground)
K = io("K", Power, default=Power("VCC", voltage=VoltageRange("5V")))

if reverse_clamping_voltage.min < reverse_standoff_voltage.max:
    error(
        "reverse clamping voltage ("
        + str(reverse_clamping_voltage)
        + ") must be >= reverse standoff voltage ("
        + str(reverse_standoff_voltage)
        + ")"
    )

if hasattr(K, "voltage"):
    if K.voltage.max > reverse_clamping_voltage.min:
        error(
            "Power net voltage ("
            + str(K.voltage)
            + ") must be <= reverse clamping voltage ("
            + str(reverse_clamping_voltage)
            + ")"
        )
else:
    warn("No voltage specified for power net")


def _footprint(package: Package) -> str:
    kicad_footprints = {
        Package("DO-219AB"): "@kicad-footprints/Diode_SMD.pretty/D_SMF.kicad_mod",
        Package("DO-214AC"): "@kicad-footprints/Diode_SMD.pretty/D_SMA.kicad_mod",
        Package("DO-214AA"): "@kicad-footprints/Diode_SMD.pretty/D_SMB.kicad_mod",
        Package("DO-214AB"): "@kicad-footprints/Diode_SMD.pretty/D_SMC.kicad_mod",
    }
    return kicad_footprints[package]


Component(
    name="D",
    mpn=mpn,
    manufacturer=manufacturer,
    prefix="D",
    symbol=Symbol(library="@kicad-symbols/Device.kicad_sym", name="D_Zener"),
    footprint=File(_footprint(package)),
    properties={
        "value": format_value(reverse_standoff_voltage, "TVS Diode", peak_pulse_power),
        "package": package,
        "peak_pulse_power": peak_pulse_power,
        "reverse_standoff_voltage": reverse_standoff_voltage,
        "reverse_clamping_voltage": reverse_clamping_voltage,
    },
    type="tvs",
    pins={
        "A": A,
        "K": K,
    },
)
