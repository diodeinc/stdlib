load("./SolderJumper.zen", "pin_defs")
load("../config.zen", "config_properties", "config_unit")
load("../units.zen", "Resistance", "Voltage")
load("../utils.zen", "format_value")

# -----------------------------------------------------------------------------
# Component types
# -----------------------------------------------------------------------------

Package = enum("0201", "0402", "0603", "0805")
TemperatureCoefficient = enum("NTC", "PTC", "Unspecified")
Mount = enum("SMD")

# -----------------------------------------------------------------------------
# Component parameters
# -----------------------------------------------------------------------------

# Required
package = config("package", Package, default = Package("0603"))
value = config_unit("value", Resistance)

# Optional
mount = config("mount", Mount, default = Mount("SMD"), optional = True)
voltage = config_unit("voltage", Voltage, optional = True)
temperature_coefficient = config("temperature_coefficient", TemperatureCoefficient, default = TemperatureCoefficient("Unspecified"), optional = True)
use_us_symbols = config("use_us_symbols", bool, default = False, optional = True)

# Properties â€“ combined and normalized
properties = config_properties({
    "value": format_value(value, voltage),
    "package": package,
    "resistance": value,
    "voltage": voltage,
})

# -----------------------------------------------------------------------------
# IO ports
# -----------------------------------------------------------------------------

P1 = io("P1", Net)
P2 = io("P2", Net)

# -----------------------------------------------------------------------------
# Helper functions
# -----------------------------------------------------------------------------

def _symbol(mount: Mount, package: Package, temperature_coefficient: TemperatureCoefficient, use_us_symbols: bool):
    name = ""

    if temperature_coefficient == TemperatureCoefficient("PTC"):
        name = "Thermistor_PTC"
    elif temperature_coefficient == TemperatureCoefficient("NTC"):
        name = "Thermistor_NTC"
    else:
        name = "Thermistor"

    if use_us_symbols:
        name = name + "_US"

    return {
        "library": "@kicad-symbols/Device.kicad_sym",
        "name": name,
    }

def _footprint(mount: Mount, package: Package):
    kicad_footprints = {
        Package("0201"): "@kicad-footprints/Resistor_SMD.pretty/R_0201_0603Metric.kicad_mod",
        Package("0402"): "@kicad-footprints/Resistor_SMD.pretty/R_0402_1005Metric.kicad_mod",
        Package("0603"): "@kicad-footprints/Resistor_SMD.pretty/R_0603_1608Metric.kicad_mod",
        Package("0805"): "@kicad-footprints/Resistor_SMD.pretty/R_0805_2012Metric.kicad_mod",
    }

    if package not in kicad_footprints:
        error("Invalid package: " + str(package))

    return kicad_footprints[package]

# -----------------------------------------------------------------------------
# Component definition
# -----------------------------------------------------------------------------

Component(
    name = "TH",
    symbol = Symbol(**_symbol(mount, package, temperature_coefficient, use_us_symbols)),
    footprint = File(_footprint(mount, package)),
    prefix = "TH",
    pin_defs = {
        "P1": "1",
        "P2": "2",
    },
    pins = {
        "P1": P1,
        "P2": P2,
    },
    properties = properties,
)
