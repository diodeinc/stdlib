load("../config.zen", "config_properties", "config_unit")
load("../units.zen", "Resistance", "Voltage")

# -----------------------------------------------------------------------------
# Component types
# -----------------------------------------------------------------------------

Package = enum("0201", "0402", "0603", "0805", "1206", "1210", "2010", "2512")
Mount = enum("SMD")

# -----------------------------------------------------------------------------
# Component parameters
# -----------------------------------------------------------------------------

# Required
package = config("package", Package, convert = Package)
value = config_unit("value", Resistance)

# Optional
mount = config("mount", Mount, default = Mount("SMD"), optional = True)
voltage = config_unit("voltage", Voltage, optional = True)

# Properties â€“ combined and normalized
properties = config_properties({
    "mount": mount,
    "package": package,
    "resistance": value,
    "voltage": voltage,
})

# -----------------------------------------------------------------------------
# IO ports
# -----------------------------------------------------------------------------

P1 = io("P1", Net)
P2 = io("P2", Net)

# -----------------------------------------------------------------------------
# Helper functions
# -----------------------------------------------------------------------------

def _footprint(package: Package):
    kicad_footprints = {
        Package("0201"): File("@kicad-footprints/Resistor_SMD.pretty/R_0201_0603Metric.kicad_mod"),
        Package("0402"): File("@kicad-footprints/Resistor_SMD.pretty/R_0402_1005Metric.kicad_mod"),
        Package("0603"): File("@kicad-footprints/Resistor_SMD.pretty/R_0603_1608Metric.kicad_mod"),
        Package("0805"): File("@kicad-footprints/Resistor_SMD.pretty/R_0805_2012Metric.kicad_mod"),
        Package("1206"): File("@kicad-footprints/Resistor_SMD.pretty/R_1206_3216Metric.kicad_mod"),
        Package("1210"): File("@kicad-footprints/Resistor_SMD.pretty/R_1210_3225Metric.kicad_mod"),
        Package("2010"): File("@kicad-footprints/Resistor_SMD.pretty/R_2010_5025Metric.kicad_mod"),
        Package("2512"): File("@kicad-footprints/Resistor_SMD.pretty/R_2512_6332Metric.kicad_mod"),
    }

    if package not in kicad_footprints:
        error("Invalid package: " + str(package))

    return kicad_footprints[package]

# -----------------------------------------------------------------------------
# Component definition
# -----------------------------------------------------------------------------

Component(
    name = "R",
    symbol = Symbol(library = "@kicad-symbols/Device.kicad_sym", name = "R_US"),
    footprint = _footprint(package),
    prefix = "R",
    pin_defs = {
        "P1": "1",
        "P2": "2",
    },
    pins = {
        "P1": P1,
        "P2": P2,
    },
    properties = properties,
)
