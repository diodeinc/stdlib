load("../config.zen", "config_unit")
load("../units.zen", "Resistance", "Voltage", "Power")
load("../utils.zen", "format_value")

# -----------------------------------------------------------------------------
# Component types
# -----------------------------------------------------------------------------

Package = enum("0201", "0402", "0603", "0805", "1206", "1210", "2010", "2512")
Mount = enum("SMD")

# -----------------------------------------------------------------------------
# Component parameters
# -----------------------------------------------------------------------------

# Required
package = config("package", Package, default=Package("0603"))
value = config_unit("value", Resistance)

# Optional
mpn = config("mpn", str, optional=True)
manufacturer = config("manufacturer", str, optional=True)
mount = config("mount", Mount, default=Mount("SMD"), optional=True)
voltage = config_unit("voltage", Voltage, optional=True)
power = config_unit("power", Power, optional=True)
use_us_symbol = config("use_us_symbol", bool, default=False, optional=True)

do_not_populate = config("do_not_populate", bool, default=False)
exclude_from_bom = config("exclude_from_bom", bool, default=False)

if do_not_populate:
    warn("do_not_populate is deprecated. Use dnp instead.", kind="deprecated.do_not_populate")
if exclude_from_bom:
    warn("exclude_from_bom is deprecated. Use skip_bom instead.", kind="deprecated.exclude_from_bom")

# Properties â€“ combined and normalized
properties = {
    "value": format_value(value, voltage, power),
    "package": package,
    "resistance": value,
    "voltage": voltage,
}

if power:
    properties["power"] = power

# -----------------------------------------------------------------------------
# IO ports
# -----------------------------------------------------------------------------

P1 = io("P1", Net)
P2 = io("P2", Net)

# -----------------------------------------------------------------------------
# Helper functions
# -----------------------------------------------------------------------------


def _symbol(mount: Mount, package: Package, use_us_symbol: bool):
    if use_us_symbol:
        name = "R_US"
    else:
        name = "R"

    return {
        "library": "@kicad-symbols/Device.kicad_sym",
        "name": name,
    }


def _footprint(mount: Mount, package: Package):
    kicad_footprints = {
        Package("0201"): "@kicad-footprints/Resistor_SMD.pretty/R_0201_0603Metric.kicad_mod",
        Package("0402"): "@kicad-footprints/Resistor_SMD.pretty/R_0402_1005Metric.kicad_mod",
        Package("0603"): "@kicad-footprints/Resistor_SMD.pretty/R_0603_1608Metric.kicad_mod",
        Package("0805"): "@kicad-footprints/Resistor_SMD.pretty/R_0805_2012Metric.kicad_mod",
        Package("1206"): "@kicad-footprints/Resistor_SMD.pretty/R_1206_3216Metric.kicad_mod",
        Package("1210"): "@kicad-footprints/Resistor_SMD.pretty/R_1210_3225Metric.kicad_mod",
        Package("2010"): "@kicad-footprints/Resistor_SMD.pretty/R_2010_5025Metric.kicad_mod",
        Package("2512"): "@kicad-footprints/Resistor_SMD.pretty/R_2512_6332Metric.kicad_mod",
    }

    if package not in kicad_footprints:
        error("Invalid package: " + str(package))

    return kicad_footprints[package]


# -----------------------------------------------------------------------------
# Component definition
# -----------------------------------------------------------------------------

Component(
    name="R",
    mpn=mpn,
    manufacturer=manufacturer,
    symbol=Symbol(**_symbol(mount, package, use_us_symbol)),
    footprint=File(_footprint(mount, package)),
    prefix="R",
    pin_defs={
        "P1": "1",
        "P2": "2",
    },
    pins={
        "P1": P1,
        "P2": P2,
    },
    spice_model=SpiceModel(
        "//simulation/Resistor.lib",
        "R",
        nets=[P1, P2],
        args={"RVAL": str(value.value)},
    ),
    properties=properties,
    type="resistor",
    dnp=do_not_populate,
    skip_bom=exclude_from_bom,
)
