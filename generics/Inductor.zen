load("../config.zen", "config_properties", "config_unit")
load("../units.zen", "Current", "Inductance")

# -----------------------------------------------------------------------------
# Component types
# -----------------------------------------------------------------------------

Package = enum("0201", "0402", "0603", "0805", "1206", "1210", "2010", "2512")
Mount = enum("SMD")

# -----------------------------------------------------------------------------
# Component parameters
# -----------------------------------------------------------------------------

# Required
package = config("package", Package)
value = config_unit("value", Inductance)

# Optional
mount = config("mount", Mount, default = Mount("SMD"), optional = True)
current = config_unit("current", Current, optional = True)

# Properties â€“ combined and normalized
properties = config_properties({
    "mount": mount,
    "value": value,
    "package": package,
    "inductance": value,
    "current": current,
})

# -----------------------------------------------------------------------------
# IO ports
# -----------------------------------------------------------------------------

P1 = io("P1", Net)
P2 = io("P2", Net)

# -----------------------------------------------------------------------------
# Helper functions
# -----------------------------------------------------------------------------

def _symbol(package: Package):
    return Symbol(library = "@kicad-symbols/Device.kicad_sym", name = "L")

def _footprint(package: Package) -> str:
    """Returns the appropriate inductor footprint based on the package."""
    footprints = {
        Package("0201"): File("@kicad-footprints/Inductor_SMD.pretty/L_0201_0603Metric.kicad_mod"),
        Package("0402"): File("@kicad-footprints/Inductor_SMD.pretty/L_0402_1005Metric.kicad_mod"),
        Package("0603"): File("@kicad-footprints/Inductor_SMD.pretty/L_0603_1608Metric.kicad_mod"),
        Package("0805"): File("@kicad-footprints/Inductor_SMD.pretty/L_0805_2012Metric.kicad_mod"),
        Package("1206"): File("@kicad-footprints/Inductor_SMD.pretty/L_1206_3216Metric.kicad_mod"),
        Package("1210"): File("@kicad-footprints/Inductor_SMD.pretty/L_1210_3225Metric.kicad_mod"),
        Package("2010"): File("@kicad-footprints/Inductor_SMD.pretty/L_2010_5025Metric.kicad_mod"),
        Package("2512"): File("@kicad-footprints/Inductor_SMD.pretty/L_2512_6332Metric.kicad_mod"),
    }

    if package not in footprints:
        error("Invalid package: " + str(package))

    return footprints[package]

# -----------------------------------------------------------------------------
# Component definition
# -----------------------------------------------------------------------------

Component(
    name = "L",
    symbol = _symbol(package),
    footprint = _footprint(package),
    prefix = "L",
    pin_defs = {
        "P1": "1",
        "P2": "2",
    },
    pins = {
        "P1": P1,
        "P2": P2,
    },
    properties = properties,
)
